plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

def isCI = (System.getenv("CI") ?: "false").toBoolean()

def twitter_consumer_key =
        isCI ? System.getenv("twitter_consumer_key") : project.property("twitter_consumer_key")
def twitter_consumer_secret =
        isCI ? System.getenv("twitter_consumer_secret") : project.property("twitter_consumer_secret")
def encrypt_name =
        isCI ? System.getenv("encrypt_name") : project.property("encrypt_name")

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.seki.saezurishiki"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName "1.2.0"

        buildConfigField "String", "TWITTER_CONSUMER_KEY", "\"$twitter_consumer_key\""
        buildConfigField "String", "TWITTER_CONSUMER_SECRET", "\"$twitter_consumer_secret\""
        buildConfigField "String", "ENCRYTP_NAME", "\"$encrypt_name\""
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }

        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }

    lintOptions {
        abortOnError false
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            variant.outputs.all {
                def applicationId = defaultConfig.applicationId
                def versionCode = defaultConfig.versionCode
                def versionName = defaultConfig.versionName
                outputFileName = "${applicationId}_${versionCode}_${versionName}.apk"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    def support_version = "28.0.0"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:preference-v7:$support_version"

    def twitter4j_version = "4.0.4"
    implementation "org.twitter4j:twitter4j-core:$twitter4j_version"
    implementation "org.twitter4j:twitter4j-stream:$twitter4j_version"
    implementation "org.twitter4j:twitter4j-media-support:$twitter4j_version"
    implementation "org.twitter4j:twitter4j-async:$twitter4j_version"

    def picasso_version = "2.3.3"
    implementation "com.squareup.picasso:picasso:$picasso_version"

    def conceal_version = "1.1.3"
    implementation "com.facebook.conceal:conceal:$conceal_version@aar"
    
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
